// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // zero or x/y
    Mux16(a=x, b=false, sel=zx, out=xOrZero);
    Mux16(a=y, b=false, sel=zy, out=yOrZero);
    // Not ? 
    Not16(in=xOrZero, out=notXorZero);
    Not16(in=yOrZero, out=notYorZero);
    Mux16(a=xOrZero, b=notXorZero, sel=nx, out=param1);
    Mux16(a=yOrZero, b=notYorZero, sel=ny, out=param2);
    // add or and
    Add16(a=param1, b=param2, out=sum);
    And16(a=param1, b=param2, out=and);
    Mux16(a=and, b=sum, sel=f, out=sumOrAnd);
    // not(Output) ?
    Not16(in=sumOrAnd, out=NotsumOrAnd);
    Mux16(a=sumOrAnd, b=NotsumOrAnd, sel=no, out=outB4Test);
    // setting control bits
    Or16Way(in=outB4Test, out=orResult);
    Not(in=orResult, out=zr);
    GetBits(in=outB4Test, out[15]=lastBit);
    And(a=lastBit, b=true, out=ng);
    And16(a=outB4Test, b=true, out=out);
}